{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "level-vision",
   "metadata": {},
   "source": [
    "---\n",
    "title: Space Shuttle Reentry Trajectory\n",
    "---"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "european-romania",
   "metadata": {},
   "source": [
    "**Originally Contributed by**: Henrique Ferrolho"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "automatic-marine",
   "metadata": {},
   "source": [
    "This tutorial demonstrates how to compute a reentry trajectory for the [Space Shuttle](https://en.wikipedia.org/wiki/Space_Shuttle), by formulating and solving a nonlinear programming problem.\n",
    "The problem was drawn from Chapter 6 of [\"Practical Methods for Optimal Control and Estimation Using Nonlinear Programming\"](https://epubs.siam.org/doi/book/10.1137/1.9780898718577), by John T. Betts."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "national-senator",
   "metadata": {},
   "source": [
    "The motion of the vehicle is defined by the following set of DAEs:\n",
    "\n",
    "$$\n",
    "\\begin{align}\n",
    "\\dot{h} & = v \\sin \\gamma , \\\\\n",
    "\\dot{\\phi} & = \\frac{v}{r} \\cos \\gamma \\sin \\psi / \\cos \\theta , \\\\\n",
    "\\dot{\\theta} & = \\frac{v}{r} \\cos \\gamma \\cos \\psi , \\\\\n",
    "\\dot{v} & = -\\frac{D}{m} - g \\sin \\gamma , \\\\\n",
    "\\dot{\\gamma} & = \\frac{L}{m v} \\cos(\\beta) + \\cos \\gamma \\left ( \\frac{v}{r} - \\frac{g}{v} \\right ) , \\\\\n",
    "\\dot{\\psi} & = \\frac{1}{m v \\cos \\gamma} L \\sin(\\beta) + \\frac{v}{r \\cos \\theta} \\cos \\gamma \\sin \\psi \\sin \\theta , \\\\\n",
    "q & \\le q_U , \\\\\n",
    "\\end{align}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "caring-novelty",
   "metadata": {},
   "source": [
    "where the aerodynamic heating on the vehicle wing leading edge is $q = q_a q_r$ and the dynamic variables are\n",
    "\n",
    "$$\n",
    "\\begin{align}\n",
    "     h & \\quad \\text{altitude (ft)},     \\qquad & & \\gamma \\quad \\text{flight path angle (rad)}, \\\\\n",
    "  \\phi & \\quad \\text{longitude (rad)},   \\qquad & & \\psi   \\quad \\text{azimuth (rad)},           \\\\\n",
    "\\theta & \\quad \\text{latitude (rad)},    \\qquad & & \\alpha \\quad \\text{angle of attack (rad)},   \\\\\n",
    "     v & \\quad \\text{velocity (ft/sec)}, \\qquad & & \\beta  \\quad \\text{bank angle (rad)}.\n",
    "\\end{align}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "wrapped-thinking",
   "metadata": {},
   "source": [
    "The aerodynamic and atmospheric forces on the vehicle are\n",
    "specified by the following quantities (English units):\n",
    "\n",
    "$$\n",
    "\\begin{align}\n",
    "           D & = \\frac{1}{2} c_D S \\rho v^2,                  \\qquad & a_0 & = -0.20704, \\\\\n",
    "           L & = \\frac{1}{2} c_L S \\rho v^2,                  \\qquad & a_1 & =  0.029244, \\\\\n",
    "           g & = \\mu / r^2,                                   \\qquad & \\mu & =  0.14076539 \\times 10^{17}, \\\\\n",
    "           r & = R_e + h,                                     \\qquad & b_0 & =  0.07854, \\\\\n",
    "        \\rho & = \\rho_0 \\exp[-h/h_r],                         \\qquad & b_1 & = -0.61592  \\times 10^{-2}, \\\\\n",
    "      \\rho_0 & = 0.002378,                                    \\qquad & b_2 & =  0.621408 \\times 10^{-3}, \\\\\n",
    "         h_r & = 23800,                                       \\qquad & q_r & =  17700 \\sqrt{\\rho} (0.0001 v)^{3.07}, \\\\\n",
    "         c_L & = a_0 + a_1 \\hat{\\alpha},                      \\qquad & q_a & =  c_0 + c_1 \\hat{\\alpha} + c_2 \\hat{\\alpha}^2 + c_3 \\hat{\\alpha}^3, \\\\\n",
    "         c_D & = b_0 + b_1 \\hat{\\alpha} + b_2 \\hat{\\alpha}^2, \\qquad & c_0 & =  1.0672181, \\\\\n",
    "\\hat{\\alpha} & = 180 \\alpha / \\pi,                            \\qquad & c_1 & = -0.19213774 \\times 10^{-1}, \\\\\n",
    "         R_e & = 20902900,                                    \\qquad & c_2 & =  0.21286289 \\times 10^{-3}, \\\\\n",
    "           S & = 2690,                                        \\qquad & c_3 & = -0.10117249 \\times 10^{-5}.\n",
    "\\end{align}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "welcome-physiology",
   "metadata": {},
   "source": [
    "The reentry trajectory begins at an altitude where the aerodynamic forces are quite small\n",
    "with a weight of $w = 203000$ (lb) and mass $m = w / g_0$ (slug), where $g_0 = 32.174$ (ft/sec$^2$). The initial conditions are as follows:\n",
    "\n",
    "$$\n",
    "\\begin{align}\n",
    "     h & = 260000 \\text{ ft},  \\qquad & v      & = 25600 \\text{ ft/sec}, \\\\\n",
    "  \\phi & = 0      \\text{ deg}, \\qquad & \\gamma & = -1    \\text{ deg},    \\\\\n",
    "\\theta & = 0      \\text{ deg}, \\qquad & \\psi   & = 90    \\text{ deg}.\n",
    "\\end{align}\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "diagnostic-profit",
   "metadata": {},
   "source": [
    "The final point on the reentry trajectory occurs at the unknown (free) time t F , at the so-called terminal area energy management (TAEM) interface, which is defined by the conditions\n",
    "\n",
    "$$\n",
    "h = 80000 \\text{ ft}, \\qquad v = 2500 \\text{ ft/sec}, \\qquad \\gamma = -5 \\text{ deg}.\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "owned-search",
   "metadata": {},
   "source": [
    "As explained in the book, our goal is to maximize the final crossrange, which is equivalent to maximizing the final latitude of the vehicle, i.e., $J = \\theta(t_F)$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "corresponding-combining",
   "metadata": {},
   "source": [
    "![Max crossrange shuttle reentry](https://i.imgur.com/1tUN3wM.png)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "accepted-conversion",
   "metadata": {},
   "source": [
    "### Approach \n",
    "We will use a discretized model of time, with a fixed number of discretized points, $n$. The decision variables at each point are going to be the state of the vehicle and the controls commanded to it. In addition, we will also make each time step size $\\Delta t$ a decision variable; that way, we can either fix the time step size easily, or allow the solver to fine-tune the duration between each adjacent pair of points. Finally, in order to approximate the derivatives of the problem dynamics, we will use either rectangular or [trapezoidal](http://en.wikipedia.org/wiki/Trapezoidal_rule) integration."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "antique-lighter",
   "metadata": {},
   "source": [
    "### Disclaimer\n",
    "Do not try to actually land a Space Shuttle using this notebook! üòõ There's no mesh refinement going on, which can lead to unrealistic trajectories having position and velocity errors with orders of magnitude $10^4$ ft and $10^2$ ft/sec, respectively."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "prescription-volume",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Interpolations, Plots\n",
    "using JuMP, Ipopt, KNITRO"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "breeding-transmission",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Global variables\n",
    "const w  = 203000  # weight (lb)\n",
    "const g‚ÇÄ = 32.174  # acceleration (ft/sec^2)\n",
    "const m  = w / g‚ÇÄ  # mass (slug)\n",
    "\n",
    "# Aerodynamic and atmospheric forces on the vehicle\n",
    "const œÅ‚ÇÄ =  0.002378\n",
    "const h·µ£ =  23800.0\n",
    "const R‚Çë =  20902900.0\n",
    "const Œº  =  0.14076539e17\n",
    "const S  =  2690.0\n",
    "const a‚ÇÄ = -0.20704\n",
    "const a‚ÇÅ =  0.029244\n",
    "const b‚ÇÄ =  0.07854\n",
    "const b‚ÇÅ = -0.61592e-2\n",
    "const b‚ÇÇ =  0.621408e-3\n",
    "const c‚ÇÄ =  1.0672181\n",
    "const c‚ÇÅ = -0.19213774e-1\n",
    "const c‚ÇÇ =  0.21286289e-3\n",
    "const c‚ÇÉ = -0.10117249e-5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "weighted-blink",
   "metadata": {},
   "outputs": [],
   "source": [
    "user_options_ipopt = (\n",
    "    \"mu_strategy\" => \"monotone\",\n",
    "    \"linear_solver\" => \"ma27\",\n",
    ")\n",
    "\n",
    "user_options_knitro = (\n",
    "    \"algorithm\" => KNITRO.KN_ALG_BAR_DIRECT,\n",
    "    \"bar_murule\" => KNITRO.KN_BAR_MURULE_QUALITY,\n",
    "    \"linsolver\" => KNITRO.KN_LINSOLVER_MA27,\n",
    ")\n",
    "\n",
    "# Create JuMP model, using Ipopt as the solver\n",
    "model = Model(optimizer_with_attributes(Ipopt.Optimizer, user_options_ipopt...))\n",
    "# model = Model(optimizer_with_attributes(KNITRO.Optimizer, user_options_knitro...))\n",
    "\n",
    "n = 2009  # number of mesh points (knots)\n",
    "\n",
    "@variables(model, begin\n",
    "               0 ‚â§ scaled_h[1:n]                # altitude (ft) / 1e5\n",
    "                          œï[1:n]                # longitude (rad)\n",
    "    deg2rad(-89) ‚â§        Œ∏[1:n] ‚â§ deg2rad(89)  # latitude (rad)\n",
    "            1e-4 ‚â§ scaled_v[1:n]                # velocity (ft/sec) / 1e4\n",
    "    deg2rad(-89) ‚â§        Œ≥[1:n] ‚â§ deg2rad(89)  # flight path angle (rad)\n",
    "                          œà[1:n]                # azimuth (rad)\n",
    "    deg2rad(-90) ‚â§        Œ±[1:n] ‚â§ deg2rad(90)  # angle of attack (rad)\n",
    "    deg2rad(-89) ‚â§        Œ≤[1:n] ‚â§ deg2rad( 1)  # banck angle (rad)\n",
    "    #        0.5 ‚â§       Œît[1:n] ‚â§ 1.5          # time step (sec)\n",
    "                         Œît[1:n] == 1.0         # time step (sec)\n",
    "end)\n",
    "\n",
    "# Initial conditions\n",
    "h‚Çõ = 2.6          # altitude (ft) / 1e5\n",
    "œï‚Çõ = deg2rad(0)   # longitude (rad)\n",
    "Œ∏‚Çõ = deg2rad(0)   # latitude (rad)\n",
    "v‚Çõ = 2.56         # velocity (ft/sec) / 1e4\n",
    "Œ≥‚Çõ = deg2rad(-1)  # flight path angle (rad)\n",
    "œà‚Çõ = deg2rad(90)  # azimuth (rad)\n",
    "Œ±‚Çõ = deg2rad(0)   # angle of attack (rad)\n",
    "Œ≤‚Çõ = deg2rad(0)   # banck angle (rad)\n",
    "t‚Çõ = 1.00         # time step (sec)\n",
    "\n",
    "# Final conditions, the so-called Terminal Area Energy Management (TAEM)\n",
    "h‚Çú = 0.8          # altitude (ft) / 1e5\n",
    "v‚Çú = 0.25         # velocity (ft/sec) / 1e4\n",
    "Œ≥‚Çú = deg2rad(-5)  # flight path angle (rad)\n",
    "\n",
    "# Fix initial conditions\n",
    "fix(scaled_h[1], h‚Çõ; force=true)\n",
    "fix(       œï[1], œï‚Çõ; force=true)\n",
    "fix(       Œ∏[1], Œ∏‚Çõ; force=true)\n",
    "fix(scaled_v[1], v‚Çõ; force=true)\n",
    "fix(       Œ≥[1], Œ≥‚Çõ; force=true)\n",
    "fix(       œà[1], œà‚Çõ; force=true)\n",
    "\n",
    "# Fix final conditions\n",
    "fix(scaled_h[n], h‚Çú; force=true)\n",
    "fix(scaled_v[n], v‚Çú; force=true)\n",
    "fix(       Œ≥[n], Œ≥‚Çú; force=true)\n",
    "\n",
    "# Initial guess: linear interpolation between boundary conditions\n",
    "x‚Çõ = [h‚Çõ, œï‚Çõ, Œ∏‚Çõ, v‚Çõ, Œ≥‚Çõ, œà‚Çõ, Œ±‚Çõ, Œ≤‚Çõ, t‚Çõ]\n",
    "x‚Çú = [h‚Çú, œï‚Çõ, Œ∏‚Çõ, v‚Çú, Œ≥‚Çú, œà‚Çõ, Œ±‚Çõ, Œ≤‚Çõ, t‚Çõ]\n",
    "interp_linear = LinearInterpolation([1, n], [x‚Çõ, x‚Çú])\n",
    "initial_guess = mapreduce(transpose, vcat, interp_linear.(1:n))\n",
    "set_start_value.(all_variables(model), vec(initial_guess))\n",
    "\n",
    "# Functions to restore `h` and `v` to their true scale\n",
    "@NLexpression(model, h[j = 1:n], scaled_h[j] * 1e5)\n",
    "@NLexpression(model, v[j = 1:n], scaled_v[j] * 1e4)\n",
    "\n",
    "# Helper functions\n",
    "# @NLexpression(model, c_L[j = 1:n], a‚ÇÄ + a‚ÇÅ * rad2deg(Œ±[j]))\n",
    "# @NLexpression(model, c_D[j = 1:n], b‚ÇÄ + b‚ÇÅ * rad2deg(Œ±[j]) + b‚ÇÇ * rad2deg(Œ±[j])^2)\n",
    "@NLexpression(model, c_L[j = 1:n], a‚ÇÄ + a‚ÇÅ * (180 * Œ±[j] / œÄ))\n",
    "@NLexpression(model, c_D[j = 1:n], b‚ÇÄ + b‚ÇÅ * (180 * Œ±[j] / œÄ) + b‚ÇÇ * (180 * Œ±[j] / œÄ)^2)\n",
    "@NLexpression(model,   œÅ[j = 1:n], œÅ‚ÇÄ * exp(-h[j] / h·µ£))\n",
    "@NLexpression(model,   D[j = 1:n], 0.5 * c_D[j] * S * œÅ[j] * v[j]^2)\n",
    "@NLexpression(model,   L[j = 1:n], 0.5 * c_L[j] * S * œÅ[j] * v[j]^2)\n",
    "@NLexpression(model,   r[j = 1:n], R‚Çë + h[j])\n",
    "@NLexpression(model,   g[j = 1:n], Œº / r[j]^2)\n",
    "\n",
    "# Motion of the vehicle as a differential-algebraic system of equations (DAEs)\n",
    "@NLexpression(model, Œ¥h[j = 1:n], v[j] * sin(Œ≥[j]))\n",
    "@NLexpression(model, Œ¥œï[j = 1:n], (v[j] / r[j]) * cos(Œ≥[j]) * sin(œà[j]) / cos(Œ∏[j]))\n",
    "@NLexpression(model, Œ¥Œ∏[j = 1:n], (v[j] / r[j]) * cos(Œ≥[j]) * cos(œà[j]))\n",
    "@NLexpression(model, Œ¥v[j = 1:n], -(D[j] / m) - g[j] * sin(Œ≥[j]))\n",
    "@NLexpression(model, Œ¥Œ≥[j = 1:n], (L[j] / (m * v[j])) * cos(Œ≤[j]) + cos(Œ≥[j]) * ((v[j] / r[j]) - (g[j] / v[j])))\n",
    "@NLexpression(model, Œ¥œà[j = 1:n], (1 / (m * v[j] * cos(Œ≥[j]))) * L[j] * sin(Œ≤[j]) + (v[j] / (r[j] * cos(Œ∏[j]))) * cos(Œ≥[j]) * sin(œà[j]) * sin(Œ∏[j]))\n",
    "\n",
    "# System dynamics\n",
    "for j in 2:n\n",
    "    i = j - 1  # index of previous knot\n",
    "\n",
    "    # Rectangular integration\n",
    "    @NLconstraint(model, h[j] == h[i] + Œît[i] * Œ¥h[i])\n",
    "    @NLconstraint(model, œï[j] == œï[i] + Œît[i] * Œ¥œï[i])\n",
    "    @NLconstraint(model, Œ∏[j] == Œ∏[i] + Œît[i] * Œ¥Œ∏[i])\n",
    "    @NLconstraint(model, v[j] == v[i] + Œît[i] * Œ¥v[i])\n",
    "    @NLconstraint(model, Œ≥[j] == Œ≥[i] + Œît[i] * Œ¥Œ≥[i])\n",
    "    @NLconstraint(model, œà[j] == œà[i] + Œît[i] * Œ¥œà[i])\n",
    "\n",
    "    # # Trapezoidal integration\n",
    "    # @NLconstraint(model, h[j] == h[i] + 0.5 * Œît[i] * (Œ¥h[j] + Œ¥h[i]))\n",
    "    # @NLconstraint(model, œï[j] == œï[i] + 0.5 * Œît[i] * (Œ¥œï[j] + Œ¥œï[i]))\n",
    "    # @NLconstraint(model, Œ∏[j] == Œ∏[i] + 0.5 * Œît[i] * (Œ¥Œ∏[j] + Œ¥Œ∏[i]))\n",
    "    # @NLconstraint(model, v[j] == v[i] + 0.5 * Œît[i] * (Œ¥v[j] + Œ¥v[i]))\n",
    "    # @NLconstraint(model, Œ≥[j] == Œ≥[i] + 0.5 * Œît[i] * (Œ¥Œ≥[j] + Œ¥Œ≥[i]))\n",
    "    # @NLconstraint(model, œà[j] == œà[i] + 0.5 * Œît[i] * (Œ¥œà[j] + Œ¥œà[i]))\n",
    "end\n",
    "\n",
    "# Objective: Maximize crossrange\n",
    "@objective(model, Max, Œ∏[n])\n",
    "\n",
    "# Solve for the control and state\n",
    "status = optimize!(model)\n",
    "\n",
    "# Show final crossrange of the solution\n",
    "println(\"Final latitude Œ∏ = \", round(objective_value(model) |> rad2deg, digits = 2), \"¬∞\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "confidential-dining",
   "metadata": {},
   "outputs": [],
   "source": [
    "ts = cumsum([0; value.(Œît)])[1:end-1]\n",
    "\n",
    "plt_altitude = plot(ts, value.(scaled_h), legend = nothing, title = \"Altitude (100,000 ft)\")\n",
    "plt_longitude = plot(ts, rad2deg.(value.(œï)), legend = nothing, title = \"Longitude (deg)\")\n",
    "plt_latitude = plot(ts, rad2deg.(value.(Œ∏)), legend = nothing, title = \"Latitude (deg)\")\n",
    "plt_velocity = plot(ts, value.(scaled_v), legend = nothing, title = \"Velocity (1000 ft/sec)\")\n",
    "plt_flight_path = plot(ts, rad2deg.(value.(Œ≥)), legend = nothing, title = \"Flight Path (deg)\")\n",
    "plt_azimuth = plot(ts, rad2deg.(value.(œà)), legend = nothing, title = \"Azimuth (deg)\")\n",
    "\n",
    "plt = plot(plt_altitude,  plt_velocity, plt_longitude, plt_flight_path, plt_latitude,\n",
    "           plt_azimuth, layout=grid(3, 2), linewidth=2, size=(700, 700))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bulgarian-climb",
   "metadata": {},
   "outputs": [],
   "source": [
    "function q(h, v, a)\n",
    "    œÅ(h) = œÅ‚ÇÄ * exp(-h / h·µ£)\n",
    "    q·µ£(h, v) = 17700 * ‚àöœÅ(h) * (0.0001 * v)^3.07\n",
    "    q‚Çê(a) = c‚ÇÄ + c‚ÇÅ * rad2deg(a) + c‚ÇÇ * rad2deg(a)^2 + c‚ÇÉ * rad2deg(a)^3    \n",
    "    # Aerodynamic heating on the vehicle wing leading edge\n",
    "    q‚Çê(a) * q·µ£(h, v)\n",
    "end\n",
    "\n",
    "plt_attack_angle = plot(ts[1:end-1], rad2deg.(value.(Œ±)[1:end-1]), legend=nothing, title=\"Angle of Attack (deg)\")\n",
    "plt_bank_angle = plot(ts[1:end-1], rad2deg.(value.(Œ≤)[1:end-1]), legend=nothing, title=\"Bank Angle (deg)\")\n",
    "plt_heating = plot(ts, q.(value.(scaled_h)*1e5, value.(scaled_v)*1e4, value.(Œ±)), legend=nothing, title=\"Heating (BTU/ft/ft/sec)\") \n",
    "\n",
    "plt = plot(plt_attack_angle, plt_bank_angle, plt_heating, layout=grid(3, 1), linewidth=2, size=(700, 700))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "abroad-voice",
   "metadata": {},
   "outputs": [],
   "source": [
    "plt = plot(rad2deg.(value.(œï)), rad2deg.(value.(Œ∏)), value.(scaled_h), linewidth=2,\n",
    "           legend=nothing, title=\"Space Shuttle Reentry Trajectory\",\n",
    "           xlabel=\"Longitude (deg)\", ylabel=\"Latitude (deg)\", zlabel=\"Altitude (100,000 ft)\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.2",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
